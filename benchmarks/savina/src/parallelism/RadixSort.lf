/**
 * Copyright (C) 2021 UC Berkeley and TU Dresden
 *
 * For more details on this benchmark, see the Cpp version from which it was derived:
 * https://github.com/lf-lang/benchmarks-lingua-franca/blob/main/Cpp/Savina/src/parallelism/RadixSort.lf.
 */
target C {
  /* [[[cog
    if (threading=="True"):
        cog.outl("single-threaded: false,")
        cog.outl(f"workers: {workers},")
        cog.outl(f"scheduler: {scheduler},")
    else:
        cog.outl("single-threaded: true,")
  ]]] */ // keep-format
  //single-threaded: true,
  /* [[[end]]] */ // keep-format
  files: ["../include/PseudoRandom.h", "../include/benchmark_runner.h"],
  //fast: true
  scheduler: STATIC,
  timeout: 12000100 nsec
}

import BenchmarkRunner from "../BenchmarkRunner.lf"

preamble {=
  #include "benchmark_runner.h"
  #include "PseudoRandom.h"
=}

reactor IntSourceReactor(
    numValues: size_t = 100000,
    maxValue: {= long =} = {= 1l << 60 =},
    seed: {= long =} = 2048) {
  state numValuesSent: size_t = 0
  state random: PseudoRandom

  input start: bool
  output outValue: {= long =}

  //logical action sendRandomNum
  input sendRandomNumIn: int;
  output sendRandomNumOut: int;

  reaction(startup) {=
    initPseudoRandom(&self->random, self->seed);
  =}

  reaction(start) -> sendRandomNumOut {=
    // Initialize the random state variable
    lf_print("send random number");

    // reset local state
    self->numValuesSent = 0;

    // start execution
    lf_set(sendRandomNumOut, 0);
  =}

  reaction(sendRandomNumIn) -> outValue, sendRandomNumOut {=
    if(self->numValuesSent < self->numValues) {
         long candidate = labs(nextLong(&self->random)) % self->maxValue;
         //printf("%ld",candidate);
         lf_set(outValue, candidate);
         self->numValuesSent += 1;
         lf_set(sendRandomNumOut,0);
    }
  =}
}

reactor Sorter(numValues: size_t = 100000, bank_index: size_t = -1) {
  state orderingArray: {= long[] =}
  state valuesSoFar: size_t = 0
  state radix: {= long =} = -1
  state pos: size_t = 0
  state sendPos: size_t = 0

  input inValue: {= long =}
  output outValue: {= long =}

  //logical action sendBufferedValues
  input sendBufferedValuesIn: int;
  output sendBufferedValuesOut: int;

  reaction(startup) {=
    self->radix = 1l << self->bank_index;
    self->orderingArray = calloc(self->numValues, sizeof(long));
  =}

  reaction(inValue) -> outValue, sendBufferedValuesOut {=
    self->valuesSoFar += 1;
    long current = inValue->value;
    if((current & self->radix) == 0) {
        // number with leading 0 (small) go through
        lf_set(outValue, current);
    } else {
        self->orderingArray[self->pos] = inValue->value;
        self->pos += 1;
    }

    if(self->valuesSoFar == self->numValues) {
        lf_set(sendBufferedValuesOut,0);
    }
  =}

  reaction(sendBufferedValuesIn) -> outValue, sendBufferedValuesOut {=
    if(self->sendPos < self->pos) {
        lf_set(outValue, self->orderingArray[self->sendPos]);
        self->sendPos += 1;
        lf_set(sendBufferedValuesOut, 0);
    } else {
        // reset local state
        self->valuesSoFar = 0;
        self->pos = 0;
        self->sendPos = 0;
    }
  =}

  reaction(shutdown) {=
    free(self->orderingArray);
  =}
}

reactor ValidationReactor(numValues: int = 100000) {
  state sumSoFar: double = 0.0
  state valuesSoFar: int = 0
  state prevValue: {= long =} = 0
  state errorValueLong: {= long =} = -1
  state errorValueInt: int = -1

  input inValue: {= long =}
  output finished: bool

  reaction(inValue) -> finished {=
    long value = inValue->value;
    self->valuesSoFar += 1;
    if(value < self->prevValue && self->errorValueLong < 0) {
        self->errorValueLong = value;
        self->errorValueInt = self->valuesSoFar - 1;
    }
    self->prevValue = value;
    self->sumSoFar += self->prevValue;

    if(self->valuesSoFar == self->numValues) {
        if(self->errorValueLong >= 0) {
            printf("ERROR: Value out of place: %ld at index %d", self->errorValueLong, self->errorValueInt);
        } else {
            printf("Elements sum: %f\n" , self->sumSoFar);
        }

        // reset local state
        self->sumSoFar = 0.0;
        self->valuesSoFar = 0;
        self->prevValue = 0;
        self->errorValueLong = -1;
        self->errorValueInt = -1;
        lf_set(finished, true);
    }
  =}
}

/* [[[cog
    import math
    numBits = int(math.log2(int(maxValue))) # this always rounds down
    cog.outl(f'main reactor RadixSort(numIterations:int={numIterations}, dataSize:int={dataSize}, maxValue:long={maxValue}, numBits:unsigned={numBits}, seed:long={seed})')
]]] */ // keep-format
main reactor (
    numIterations: int = 12,
    dataSize: int = 8,
    maxValue: long = 8,
    numBits: unsigned = 3,
    seed: long = 2048)
/* [[[end]]] */ // keep-format    
{
  runner = new BenchmarkRunner(num_iterations=numIterations)
  runner.nextIterationOut -> runner.nextIterationIn after 1nsec;
  runner.doneOut -> runner.doneIn after 1nsec;
  source = new IntSourceReactor(numValues=dataSize, maxValue=maxValue, seed=seed)
  source.sendRandomNumOut -> source.sendRandomNumIn after 1nsec;
  validator = new ValidationReactor(numValues=dataSize)
  sorter0 = new Sorter(numValues=dataSize, bank_index=0)
  sorter1 = new Sorter(numValues=dataSize, bank_index=1)
  sorter2 = new Sorter(numValues=dataSize, bank_index=2)
  sorter3 = new Sorter(numValues=dataSize, bank_index=2)

  sorter0.sendBufferedValuesOut -> sorter0.sendBufferedValuesIn after 1nsec;
  sorter1.sendBufferedValuesOut -> sorter1.sendBufferedValuesIn after 1nsec;
  sorter2.sendBufferedValuesOut -> sorter2.sendBufferedValuesIn after 1nsec;
  sorter3.sendBufferedValuesOut -> sorter3.sendBufferedValuesIn after 1nsec;

  runner.start -> source.start
  source.outValue -> sorter0.inValue;
  sorter0.outValue -> sorter1.inValue;
  sorter1.outValue -> sorter2.inValue;
  sorter2.outValue -> sorter3.inValue;
  sorter3.outValue ->  validator.inValue
  validator.finished -> runner.finish

  reaction(startup) {=
    printBenchmarkInfo("RadixSortLFCBenchmark");
    printSystemInfo();
  =}
}
